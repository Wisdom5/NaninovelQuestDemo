@hideAll time:0
@back room_0 id:SpaceShip

@if !awakeningCompleted
    #Awakening
    You wake up with a throbbing headache. Yesterday's events are a blur...
    
    @char Kohaku pos:50,0
    Kohaku: Hey, are you feeling okay? You took quite a hit yesterday.
    Kohaku: By the way, do you remember your name?

    @input playerName summary:"Enter your name:"
    @stop
    You remember that your name is {playerName}.

    @char Kohaku pos:50,0
    Kohaku: It's good to have you back, {playerName}. We have a lot to discuss.
    Kohaku: Actually, we could use your help. There's another crewmate at the storage bay who needs assistance.
    Kohaku: It's nothing complicated — just sorting supplies. Maybe working will help you clear your head.

    @choice "Sure, I’ll help out." goto:.Agree
    @choice "Supplies? That doesn’t sound fun..." goto:.Lazy
    @stop

    #Agree
    @set helpedStorage=true
    @goto .AfterChoice

    #Lazy
    @set helpedStorage=false
    @goto .AfterChoice

    #AfterChoice
    @if helpedStorage
        Kohaku: Great, I knew I could count on you!
    @else
        @char Kohaku.Angrytalk
        Kohaku: Ah, don’t be lazy, {playerName}, come on. Let’s get this done.
        You kind of regret not agreeing immediately.
    @endif
    @set awakeningCompleted=true

@else
    #FinalChipChoice

    @char Kohaku pos:20,0
    @char Chinatsu pos:80,0
    You finally find the Mysterious Data Chip in the Control Room. Who should you give it to, or keep it for yourself?

    @choice "Give the chip to Chinatsu." goto:.GiveChinatsu
    @choice "Give the chip to Kohaku." goto:.GiveKohaku
    @choice "Keep the chip for yourself." goto:.KeepChip
    @stop

    #GiveChinatsu
    @set gaveChipTo="Chinatsu"
    @char Chinatsu.Happy
    Chinatsu: Thank you, {playerName}! I appreciate it. You’re the best!
    @char Kohaku.Angrytalk
    Kohaku: Hmm… really? I’m not thrilled about that.  
    @goto .AfterChipDecision

    #GiveKohaku
    @set gaveChipTo="Kohaku"
    @char Kohaku.Happy
    Kohaku: Thanks, {playerName}! I knew I could count on you.
    @char Chinatsu.Angrytalk
    Chinatsu: Really? That’s… disappointing.  
    @goto .AfterChipDecision

    #KeepChip
    @set gaveChipTo="None"
    @char Kohaku.Angrytalk
    Kohaku: {playerName}, you really are keeping it? That’s… infuriating.  
    @char Chinatsu.Angrytalk
    Chinatsu: Seriously? I didn’t expect that from you…  
    @goto .AfterChipDecision


    #AfterChipDecision
    The end
    @stop
@endif



@goto Map
@stop
